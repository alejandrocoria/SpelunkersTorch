import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.accesstransformers' version '5.0.2'
    id 'net.minecraftforge.gradle' version '7.0.0-beta.55'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
}

jar.archiveClassifier = "forge"

repositories {
    maven minecraft.mavenizer
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
    mavenCentral()
}

dependencies {
    implementation minecraft.dependency("net.minecraftforge:forge:${forge_version}")
    annotationProcessor 'net.minecraftforge:eventbus-validator:7.0-beta.10'
}

minecraft {
    mappings channel: "parchment", version: "${project.mappings_mc_version}-${project.mappings_date}"

    accessTransformers = true

    runs {
        configureEach {
            systemProperty 'forge.logging.console.level', 'debug'
            systemProperty 'eventbus.api.strictRuntimeChecks', 'true'
            args "--mixin.config=${mod_id}.mixins.json"
            systemProperty "mixin.debug.export", "true"
            systemProperty "mixin.env.remapRefMap", "true"
            systemProperty "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
        }

        register('client') {
            workingDir.convention layout.projectDirectory.dir("runs/client")
            systemProperty "mixin.debug.export", "true"
        }

        register('server') {
            workingDir.convention layout.projectDirectory.dir("runs/server")
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

jar {
    dependsOn "processDocs"
    manifest {
        attributes([
                "MixinConfigs": "${mod_id}.mixins.json",
        ])
    }
}

tasks.register('processDocs', Copy) {
    // update docs
    from(file("../doc/")) {
        include '*.md'
        expand 'loaderversion': project.forge_version,
                'loadername': "Forge"
    }
    into 'build/doc'
}

tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

if (!project.hasProperty("curseForgeApiKey")) {
    ext.curseForgeApiKey = project.findProperty("curseforge_api_key") ?: "notset"
}
if (!project.hasProperty("modrinthApiKey")) {
    ext.modrinthApiKey = project.findProperty("modrinth_api_key") ?: "notset"
}

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = "${curseForgeApiKey}"
    def mainFile = upload(curseforge_project_id, jar)
    mainFile.displayName = "${mod_name}-${minecraft_version}-${version}-forge"
    mainFile.releaseType = release_type
    mainFile.addModLoader("Forge")
    mainFile.addJavaVersion("Java 21")
    mainFile.addGameVersion("${project.minecraft_version}")
    mainFile.addEnvironment("Server", "Client")
    mainFile.changelogType = "markdown"
    mainFile.changelog = file("$project.buildDir/doc/changelog.md")
}

tasks.modrinth.dependsOn(tasks.build)

modrinth {
//    debugMode.set(true)
    token = "${modrinthApiKey}"
    projectId = modrinth_project_id
    versionNumber = "${minecraft_version}-${version}-forge"
    versionName = "${mod_name}-${minecraft_version}-${version}-forge"
    versionType = release_type
    uploadFile = jar
    gameVersions = ["${project.minecraft_version}"]
    loaders = ["forge"]
    changelog = file("$project.buildDir/doc/changelog.md").exists() ? file("$project.buildDir/doc/changelog.md").text : null
}

project.afterEvaluate {
    project.tasks.findAll { task -> (task.name.startsWith('curseforge') || task.name.startsWith('modrinth'))
    }.each {
        projectTask -> projectTask.dependsOn build
    }
}
